apply plugin: 'com.android.application'

def secretsPropertiesFile = rootProject.file("secrets.properties")
def secretsProperties = new Properties()
secretsProperties.load(new FileInputStream(secretsPropertiesFile))

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.hrst.temi_mqtt_client"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 2
        versionName "1.0.2"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // secrets
        // @TODO: Doing this because it's easier for developer. Consider using Android Keystore API
        // Ref: https://github.com/codepath/android_guides/wiki/Storing-Secret-Keys-in-Android
        buildConfigField("String", "MQTT_HOSTNAME", secretsProperties['MQTT_HOSTNAME'])
        buildConfigField("String", "MQTT_USERNAME", secretsProperties['MQTT_USERNAME'])
        buildConfigField("String", "MQTT_PASSWORD", secretsProperties['MQTT_PASSWORD'])
    }
    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            // Caution! In production, you need to generate your own keystore file.
            // see https://facebook.github.io/react-native/docs/signed-apk-android.
            signingConfig signingConfigs.debug
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    maven { url "https://repo.eclipse.org/content/repositories/paho-releases/" }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.0.0'

    // paho-mqtt
    // https://github.com/eclipse/paho.mqtt.java
    implementation('org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.2.5')
    // https://github.com/eclipse/paho.mqtt.android
    implementation('org.eclipse.paho:org.eclipse.paho.android.service:1.1.1')

    // temi
    // https://github.com/robotemi/sdk
    implementation 'com.robotemi:sdk:0.10.77'
}
